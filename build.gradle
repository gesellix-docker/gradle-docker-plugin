import java.text.SimpleDateFormat

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

subprojects {
    ext {
        artifactVersion = new SimpleDateFormat('yyyy-MM-dd\'T\'HH-mm-ss').format(new Date())
        bintrayDryRun = false
    }

    apply plugin: 'groovy'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    def dependencyVersions = [
            'com.squareup.okio:okio:2.1.0',
            'org.jetbrains.kotlin:kotlin-reflect:1.3.10',
            'org.jetbrains.kotlin:kotlin-stdlib:1.3.10',
    ]

    def dependencyVersionsByGroup = [
            'org.codehaus.groovy': '2.5.2'
    ]

    configurations.all {
        resolutionStrategy {
            failOnVersionConflict()
            force dependencyVersions
            eachDependency { DependencyResolveDetails details ->
                def forcedVersion = dependencyVersionsByGroup[details.requested.group]
                if (forcedVersion) {
                    details.useVersion(forcedVersion)
                }
            }
        }
    }
}

task updateWrapper(type: Wrapper) {
    gradleVersion = '5.0'
    distributionType = Wrapper.DistributionType.ALL
}
